
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.РасписаниеЗанятий.Записывать = Истина;
	Для Каждого ТекСтрокаУроки Из Уроки Цикл
		Движение = Движения.РасписаниеЗанятий.Добавить();
		Движение.Период					= ТекСтрокаУроки.ДатаНачала;
		Движение.Обучающийся			= ТекСтрокаУроки.Обучающийся;
		Движение.Длительность			= ТекСтрокаУроки.Длительность;
		Движение.Дисциплина				= Дисциплина;
		Движение.Замечание				= ТекСтрокаУроки.Замечание;
		Движение.ТемаПреподавателя		= ТемаПреподователя;
		Движение.ТемаПоУчебнику			= ТемаПоУчебнику;
		Движение.ДомашнееЗадание		= ДомашнееЗадание;
		Движение.Оценка					= ТекСтрокаУроки.Оценка;
		Движение.Преподаватель			= Преподаватель;
		Движение.ИндивидуальноеЗанятие	= ИндивидуальноеЗанятие;
		Движение.Сумма					= ТекСтрокаУроки.Сумма;
		Движение.Занятие				= Ссылка;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если НЕ РегистрыСведений.СтатусыЗанятий.НайтиСтатусЗанятия(Ссылка) Тогда
		РегистрыСведений.СтатусыЗанятий.ЗаписатьСтатусЗанятия(Ссылка, Перечисления.СтатусыЗанятий.НеПодтверждено)
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Отказ = КонфликтМеждуЗанятиями();
	Если Отказ Тогда
		Сообщить("Некорректные данные в табличной части ""Уроки""");
	КонецЕсли;
	
КонецПроцедуры

Функция КонфликтМеждуЗанятиями()
	
	УрокиСвернутая = Уроки.Выгрузить();
	УченикиСвернутая = УрокиСвернутая.Скопировать();
	УченикиСвернутая.Свернуть("Обучающийся");
	УрокиСвернутая.Свернуть("ДатаНачала");
	Если УрокиСвернутая.Количество() > 1 ИЛИ УченикиСвернутая.Количество() <> Уроки.Количество() Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;		
	
КонецФункции