
#Область ПрограммныйИнтерфейс

// Возвращает настройку обращения к методу GPT
//
// Параметры:
//	Метод - СправочникСсылка.МетодыGPT - Ссылка на метод
//
// Возвращаемое значение:
//	Структура - Настройки вызова метода
//
Функция НастройкаМетода(Метод) Экспорт

	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	НастройкиВызоваМетодовGPT.АдресРесурса КАК АдресРесурса,
	                      |	НастройкиВызоваМетодовGPT.Модель КАК Модель,
	                      |	НастройкиВызоваМетодовGPT.Таймаут КАК Таймаут,
	                      |	НастройкиВызоваМетодовGPT.УровеньКреативности КАК УровеньКреативности,
	                      |	НастройкиВызоваМетодовGPT.КоличествоВариантовОтвета КАК КоличествоВариантовОтвета
	                      |ИЗ
	                      |	РегистрСведений.НастройкиВызоваМетодовGPT КАК НастройкиВызоваМетодовGPT
	                      |ГДЕ
	                      |	НастройкиВызоваМетодовGPT.Используется
	                      |	И НастройкиВызоваМетодовGPT.Метод = &Метод");
	
	Запрос.УстановитьПараметр("Метод", Метод);
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда		
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Настройка = СтруктураНастройки();
	ЗаполнитьЗначенияСвойств(Настройка, Выборка);
	
	Возврат Настройка;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтруктураНастройки()
	
	Настройка = Новый Структура;
	Настройка.Вставить("АдресРесурса", "");
	Настройка.Вставить("Модель", "");
	Настройка.Вставить("Таймаут", "");
	Настройка.Вставить("УровеньКреативности", 0); 
	Настройка.Вставить("КоличествоВариантовОтвета", 0);
	
	Возврат Настройка;
	
КонецФункции

#КонецОбласти