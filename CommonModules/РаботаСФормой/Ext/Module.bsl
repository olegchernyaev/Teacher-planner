
#Область ПрограммныйИнтерфейс

// Условное оформление

Функция ДобавитьГруппуЭлементовОтбораКомпоновкиДанных(ГруппаЭлементовОтбора, ТипГруппы) Экспорт
	
	ГруппаОтборов = ГруппаЭлементовОтбора.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	Если ТипЗнч(ТипГруппы)=Тип("Строка") Тогда
		ГруппаОтборов.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных[ТипГруппы];
	Иначе
		ГруппаОтборов.ТипГруппы = ТипГруппы;
	КонецЕсли; 
	Возврат ГруппаОтборов;
	
КонецФункции

Процедура ДобавитьЭлементОтбораКомпоновкиДанных(ГруппаЭлементовОтбора, ПутьКДаннымПоля, Значение, ВидСравнения = Неопределено) Экспорт
	
	Если ВидСравнения = Неопределено Тогда
		ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ИначеЕсли ТипЗнч(ВидСравнения)=Тип("Строка") Тогда
		ВидСравнения = ВидСравненияКомпоновкиДанных[ВидСравнения];
	КонецЕсли;
	
	Отбор = ГруппаЭлементовОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения   = ВидСравнения;
	Отбор.Использование  = Истина;
	Отбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(ПутьКДаннымПоля);
	Отбор.ПравоеЗначение = Значение;
	
КонецПроцедуры

Процедура ДобавитьОформляемоеПоле(УсловноеОформление, ПутьКДаннымПоля) Экспорт
	
	ОформляемоеПоле                = УсловноеОформление.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле           = Новый ПолеКомпоновкиДанных(ПутьКДаннымПоля);
	ОформляемоеПоле.Использование  = Истина;
	
КонецПроцедуры

Процедура ДобавитьОформляемыеПоля(УсловноеОформление, ПоляКомпоновкиДанных) Экспорт
	
	Если ТипЗнч(ПоляКомпоновкиДанных)=Тип("Массив") Тогда
		МассивПолей = ПоляКомпоновкиДанных;
	ИначеЕсли ТипЗнч(ПоляКомпоновкиДанных)=Тип("Строка") Тогда
		МассивПолей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрЗаменить(ПоляКомпоновкиДанных, Символы.ПС, ""));
	Иначе
		Возврат;
	КонецЕсли;
	
	Для каждого ИмяПоля Из МассивПолей Цикл
		ДобавитьОформляемоеПоле(УсловноеОформление, ИмяПоля)
	КонецЦикла; 
	
КонецПроцедуры

Процедура ДобавитьЭлементУсловногоОформления(УсловноеОформление, Идентификатор, Значение) Экспорт
	
	Оформление = УсловноеОформление.Оформление.Элементы.Найти(Идентификатор);
	Оформление.Значение      = Значение;
	Оформление.Использование = Истина;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

#КонецОбласти 

 